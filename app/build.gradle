plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
}

apply from: '../config/version.gradle'
apply from: '../config/lint.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.superheroes"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode = getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "environment"
    productFlavors {
        staging {
            dimension "environment"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            resValue "string", "app_name", "Super Heroes S"
        }
        production {
            dimension "environment"
            applicationIdSuffix ".production"
            versionNameSuffix "-production"
            resValue "string", "app_name", "Super Heroes P"
        }
    }
    // https://kotlinlang.org/docs/tutorials/android-plugin.html
    androidExtensions {
        experimental = true
    }
    // Experimental, can cause false cache hits with certain annotation processors:
    // https://blog.jetbrains.com/kotlin/2018/02/using-gradle-build-cache-with-kotlin/#comment-45400
    kapt {
        useBuildCache = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // Dagger2
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Rx libraries
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Rx binding
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
    implementation 'com.jakewharton.rxbinding3:rxbinding-core:3.0.0-alpha2'
    implementation 'com.jakewharton.rxbinding3:rxbinding-recyclerview:3.0.0-alpha2'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.4.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation "org.mockito:mockito-core:$mockito_version"

    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1', {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Idling dependencies
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'


    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0', {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
}
